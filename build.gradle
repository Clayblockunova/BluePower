buildscript {
    repositories {
        maven { url = 'https://sizableshrimp.me/maven' }
        maven { url = 'https://files.minecraftforge.net/maven' }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: 'me.sizableshrimp.parchmenttest', name: 'ForgeGradle', version: '4.1.+', changing: true
    }
}

plugins {
    id "com.wynprice.cursemaven" version "2.1.5"
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'
apply plugin: 'maven'



project.ext {
    major = "0"
    minor = "5"
    if (System.getenv("CI_PIPELINE_IID") != null) {
        build = System.getenv("CI_PIPELINE_IID") + "-alpha"
    } else {
        build = "-custom"
    }
    mcVersion = "1.16.5"
}

version = "${project.ext.major}.${project.minor}.${project.build}"
group = "com.bluepowermod"
archivesBaseName = "bluepower"

repositories {
    maven {
        name "JEI"
        url "https://dvs1.progwml6.com/files/maven/"
    }
}

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.

minecraft {
    mappings channel: 'parchment', version: '20210608-SNAPSHOT-1.16.5'

    runs {
        client {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
            mods {
                bluepower {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
            mods {
                bluepower {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
            args '--mod', 'bluepower', '--all', '--output', file('src/generated/resources/')
            mods {
                bluepower {
                    source sourceSets.main
                }
            }
        }
    }

}

dependencies {
    minecraft 'net.minecraftforge:forge:1.16.5-36.1.32'
    //deobfCompile "li.cil.oc:OpenComputers:MC1.12.2-1.7.4.153:api"
    //compile "net.industrial-craft:industrialcraft-2:2.8.99-ex112:api"
    compileOnly fg.deobf("curse.maven:Hwyla:3033595")
    compileOnly fg.deobf("mezz.jei:jei-1.16.5:7.7.0.107:api")
    runtimeOnly fg.deobf("mezz.jei:jei-1.16.5:7.7.0.107")
}

jar {
    appendix = "${project.ext.mcVersion}"
    classifier = "universal"
}

task deobfJar(type: Jar) {
    from(sourceSets.main.output) {
        appendix = "${project.ext.mcVersion}"
        classifier = "deobf"
    }
}

// Create API library zip
task apiZip(type: Zip) {
    from(sourceSets.main.java) {
        include 'com/bluepowermod/api/**'
        appendix = "${project.ext.mcVersion}"
        classifier = "api"
    }
}

apiZip.mustRunAfter deobfJar

artifacts {
    archives deobfJar
    archives apiZip
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: "file://" + System.getenv("local_maven"))
            pom {
                groupId = project.group
                version = project.version
                artifactId = project.archivesBaseName
                project {
                    name project.archivesBaseName
                    packaging 'jar'
                    description 'BluePower'
                    url 'https://github.com/Qmunity/BluePower'
                    scm {
                        url 'https://github.com/Qmunity/BluePower'
                        connection 'scm:git:git@github.com:Qmunity/BluePower.git'
                        developerConnection 'scm:git:git@github.com:Qmunity/BluePower.git'
                    }
                    issueManagement {
                        system 'github'
                        url 'https://github.com/Qmunity/BluePower/issues'
                    }
                    licenses {
                        license {
                            name 'GPLv3'
                            url 'https://github.com/Qmunity/BluePower/blob/master/LICENCE.txt'
                            distribution 'repo'
                        }
                    }
                    developers {
                        developer {
                            id 'MineMaarten'
                            name 'MineMaarten'
                            roles { role 'developer' }
                        }
                        developer {
                            id 'Amadornes'
                            name 'Amadornes'
                            roles { role 'developer' }
                        }
                        developer {
                            id 'Quetzi'
                            name 'Quetzi'
                            roles { role 'developer' }
                        }
                        developer {
                            id 'K4Unl'
                            name 'K4Unl'
                            roles { role 'developer' }
                        }
                        developer {
                            id 'MoreThanHidden'
                            name 'MoreThanHidden'
                            roles { role 'developer' }
                        }
                        developer {
                            id 'Zness'
                            name 'Zness'
                            roles { role 'artist' }
                        }
                    }
                }
            }
        }
    }
}

jar {
    manifest {
        attributes(["Specification-Title": "bluepower",
                    "Specification-Vendor": "qmunity",
                    "Specification-Version": "28.0", // We are version 1 of the modlauncher specification
                    "Implementation-Title": project.name,
                    "Implementation-Version": "${version}",
                    "Implementation-Vendor" :"qmunity",
                    "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")],)
    }
}